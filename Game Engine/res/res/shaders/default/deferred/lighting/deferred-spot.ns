$VERSION
150 core
$END

$VS
in vec3 inPosition;
in vec2 inTexCoord;

out vec2 texCoord;
out vec3 cameraPos;
out vec3 lightPos;
//out vec3 direction;

uniform mat4 viewMatrix;
uniform mat4 modelMatrix;

void main() {
	gl_Position = vec4(inPosition, 1);
	texCoord = inTexCoord;
	cameraPos = (inverse(viewMatrix) * vec4(0, 0, 0, 1)).xyz;
	lightPos = (modelMatrix * vec4(0, 0, 0, 1)).xyz;
	//direction = normalize(modelMatrix * vec4(0, 0, 1, 0)).xyz;
}
$END

$FS
#include <DIFF:lighting.nsi>
#include <SPEC:lighting.nsi>
#include <ATTEN:lighting.nsi>

in vec2 texCoord;
in vec3 cameraPos;
in vec3 lightPos;
//in vec3 direction;

out vec4 outColor;

uniform vec3 lightColor;
uniform vec3 direction;
uniform vec4 attenuation;

uniform sampler2D colors;
uniform sampler2D positions;
uniform sampler2D normals;
uniform sampler2D specular;

void main() {
	vec3 position = texture(positions, texCoord).xyz;
	vec3 unitNormal = texture(normals, texCoord).xyz;
	vec3 eyeDir = position - lightPos;
	float dist = length(eyeDir);
	float dimmer = attenuation.x * dist * dist + attenuation.y * dist + attenuation.z;
	eyeDir = normalize(eyeDir);
	float spotFactor = dot(direction, eyeDir);
	if (attenuation.w > spotFactor) {
		spotFactor = (1.0 - (1 - spotFactor) / (1 - attenuation.w));
	}
	float diffuse = calcDiff(unitNormal, eyeDir) * spotFactor / dimmer;

	vec3 toCamera = normalize(cameraPos - position);

	vec4 specularVal = texture(specular, texCoord);

	float specularFactor = calcSpec(toCamera, eyeDir, unitNormal, specularVal.y, specularVal.x) * spotFactor / dimmer;

	outColor = vec4(texture(colors, texCoord).rgb * (diffuse * lightColor) + specularFactor * lightColor, 0) * specularVal.z;
}
$END
